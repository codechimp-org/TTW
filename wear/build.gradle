apply plugin: 'com.android.application'

project.archivesBaseName = "TTW";

android {
    // Allows you to reference product flavors in your
    // phone module's build.gradle file
    publishNonDefault true

    compileSdkVersion 24
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId "org.codechimp.ttw"
        minSdkVersion 23
        targetSdkVersion 24
        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE) + 1
    }

    signingConfigs { release }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        wear1 {
            // Use the defaultConfig value
        }
        wear2 {
            minSdkVersion 24
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.android.support:wearable:1.4.0'
    compile 'com.google.android.gms:play-services-wearable:10.0.1'
}

def Properties props = new Properties()
props.load(new FileInputStream(file('signing.properties')))

if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
        props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
    android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
    android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
    android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
    android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
} else {
    android.buildTypes.release.signingConfig = null
}