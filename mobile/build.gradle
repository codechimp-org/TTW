apply plugin: 'com.android.application'

project.archivesBaseName = "TTW";

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "org.codechimp.ttw"
        minSdkVersion 19
        targetSdkVersion 26
        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)
        wearAppUnbundled true
    }

    signingConfigs { release }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "TTW-${variant.name}-${project.VERSION_NAME}.apk"
        }
    }
}


configurations {
    wear1ReleaseWearApp
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':locale-api')

    implementation "com.google.android.gms:play-services-wearable:$rootProject.ext.playServicesVersion"
    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.ext.supportVersion"
    implementation "com.android.support:percent:$rootProject.ext.supportVersion"
    implementation "com.android.support:preference-v14:$rootProject.ext.supportVersion"
    implementation "com.android.support:design:$rootProject.ext.supportVersion"
    implementation "com.android.support:support-v4:$rootProject.ext.supportVersion"
    provided 'com.google.android.wearable:wearable:2.1.0'
    implementation 'com.google.android.support:wearable:2.1.0'
}

// Signing properties goes in project root (NOT app)
def Properties props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}