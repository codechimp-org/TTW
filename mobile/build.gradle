apply plugin: 'com.android.application'

project.archivesBaseName = "TTW-mobile";

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "org.codechimp.ttw"
        minSdkVersion 21
        targetSdkVersion 28
        versionName project.VERSION_NAME
        wearAppUnbundled true

        ext.env = System.getenv()

        if (env.BUILD_NUMBER != null) {
            versionCode Integer.parseInt("50" + env.BUILD_NUMBER + "1")
        } else {
            versionCode Integer.parseInt(project.VERSION_CODE)
        }

        versionName project.VERSION_NAME + "." + versionCode.toString()
    }

    signingConfigs { release }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

//    android.applicationVariants.all { variant ->
//        variant.outputs.all {
//            outputFileName = "TTW-${variant.name}-${project.VERSION_NAME}.apk"
//        }
//    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':locale-api')

    implementation "com.google.android.gms:play-services-wearable:$rootProject.ext.playServicesVersion"
    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.ext.supportVersion"
    implementation "com.android.support:percent:$rootProject.ext.supportVersion"
    implementation "com.android.support:preference-v14:$rootProject.ext.supportVersion"
    implementation "com.android.support:design:$rootProject.ext.supportVersion"
    implementation "com.android.support:support-v4:$rootProject.ext.supportVersion"
    compileOnly 'com.google.android.wearable:wearable:2.4.0'
    implementation 'com.google.android.support:wearable:2.4.0'
}

if (signingPropsFile.canRead()) {
    def props = new Properties()
    props.load(signingPropsFile.newReader('UTF-8'))

    if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}